apply plugin: 'maven-publish'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
ext.isPreviewVersion = version.indexOf("preview") != -1

java {
    withJavadocJar()

    if (!isPreviewVersion) {
        withSourcesJar()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            apply from: rootProject.file('pom.gradle')
            pom(pomData)
        }
    }
    tasks.withType(Sign).configureEach {
        onlyIf { isReleaseVersion && System.getenv("NO_GPG_SIGN") != "true" }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.maven
    }

    repositories {
        maven {
            name 'sonatype'
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username = findProperty("sonatype.username")
                password = findProperty("sonatype.password")
            }
        }
    }
}